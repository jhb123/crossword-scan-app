plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id("androidx.navigation.safeargs.kotlin")
}

Properties props = new Properties()
props.load(new FileInputStream("$project.rootDir/version.properties"))
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}
private Integer generateVersionCode() {
    int major = ext.majorVersion as Integer
    int minor = ext.minorVersion as Integer
    int patch = ext.patchVersion as Integer
    println(major * 10000 + minor * 100 + patch)

    return major * 10000 + minor * 100 + patch
}

private String generateVersionName() {
    if (ext.buildNumber) {
        return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}.${ext.buildNumber}"
    }

    return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
}

android {
    namespace 'com.jhb.crosswordScan'
    compileSdk 33
    //buildToolsVersion '23.0.1'

    defaultConfig {
        applicationId "com.jhb.crosswordScan"
        minSdk 24
        //multiDexEnabled true
        targetSdk 33
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "app_type"
    productFlavors {
        stage {
            buildConfigField "String", "API_URL", "\"https://stage.live-crossword.net\""
            buildConfigField "String", "WEBSOCKET_URL", "\"wss://stage.live-crossword.net\""
            dimension "app_type"
        }
        real {
            buildConfigField "String", "API_URL", "\"https://www.live-crossword.net\""
            buildConfigField "String", "WEBSOCKET_URL", "\"wss://live-crossword.net\""
            dimension "app_type"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
        compose = true

    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }
    //
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation project(path: ':openCV')
    // this would be good if it was actively maintained
    // implementation 'com.quickbirdstudios:opencv:4.5.3.0'

    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'

    // Room components
    implementation "androidx.room:room-ktx:2.5.2"
    kapt "androidx.room:room-compiler:2.5.2"

    //compose
    implementation "androidx.compose.ui:ui:1.4.3"
    implementation "androidx.compose.material3:material3:1.1.1"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation 'androidx.compose.ui:ui-tooling-preview:1.4.3'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.4.3'

    //navigation
    implementation "androidx.navigation:navigation-compose:2.6.0"

    //ml
    implementation 'com.google.mlkit:text-recognition:16.0.0'

    // Retrofit web requests
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    //serialising json strings.
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0"
    implementation 'com.google.code.gson:gson:2.10.1'

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.room:room-testing:2.5.2"
}
